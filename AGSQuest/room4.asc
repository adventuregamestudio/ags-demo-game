// room script file

//Called before room fades in
function room_Load()
{
  oChandelier.SetView(4); // give chandelier object access to animations
  oChandelier.Animate(0, 4, eRepeat, eNoBlock); // animate chandelier, looping
  
  oCupOnTable.Baseline = 360; //make sure blue cup appears in front of walk-behind area that covers table
  oCupOnTable.Scaling = 50; //make cup appear smaller than original sprite size
  
  if(player.y == 146){ // if entering from balcony
    player.Transparency = 0; // undo disappearing that happened in previous room
  }
}

//Next few functions: click on the various hotspots in the room

function hFrontExit_AnyClick()
{
  player.Walk(433, 344, eBlock, eWalkableAreas);
  player.ChangeRoom(3, 180, 254, eDirectionRight); //go outside, to specific coordinates
}

function hWashBasin_AnyClick()
{
  if(player.ActiveInventory != null){ //if player uses inventory on hotspot
      player.Say("I don't need to wash this.");
      return;
    }
  if(cBlueMage.HasInventory(iGem2)){ // if player already has the hidden gem
    player.Walk(321, 133, eNoBlock, eWalkableAreas);
    player.Say("The sink is empty.");
  }
  else{ //if player doesn't have hidden gem yet
    player.Walk(321, 133, eBlock, eWalkableAreas);
    player.FaceLocation(321, 100);
    Display("You found a hidden gem in the sink!");
    gInGameMenu.Visible = true;         // Show the inventory GUI so that
    WaitMouseKey(GetGameSpeed());       // the player sees the item when it
    player.AddInventory(iGem2);          // becomes added. Add inventory item. Play powerup sound.
    aPowerup_by_maodin204.Play();       // Then hide the GUI afterward.
    WaitMouseKey(GetGameSpeed());       // 
    gInGameMenu.Visible = false;        //
    cRedMage.Say("I don't know why I left that there.");
  }
}

function oBackDoor_AnyClick()
{
  if(player.ActiveInventory == null){
    player.Walk(200, 146, eBlock, eWalkableAreas);
    if(oBackDoor.Graphic == 47){ //if door is closed
      aDoorOpen_by_splicesound.Play(); //play sound
      oBackDoor.Graphic = 48; //siwtch to open door sprite, revealing exit hotspot behind door object
      return;
    }
    else{ //if door is open
      aDoor_Close_by_kyles.Play(); //play sound
      oBackDoor.Graphic = 47;  //switch to closed door sprite
    }
  }
  else{ //if using inventory on door
    player.Say("The door is unlocked. It doesn't need anything used on it.");
  }
}

function hCupboard_AnyClick()
{
  if(player.ActiveInventory != null){ //if player uses inventory on hotspot
    unhandled_event(1, 3);
    return;
  }
  player.Say("It's a nice-looking cupboard.");
  cRedMage.Say("It is. I inherited it from my mother.");
}

function hTable_AnyClick()
{
  if(player.ActiveInventory != null){ //if player uses inventory on hotspot
    if(player.ActiveInventory == iBlueCup){ // if using blue cup
      player.Walk(370, 290, eBlock, eWalkableAreas);
      player.FaceDirection(eDirectionDown);
      player.LoseInventory(iBlueCup);
      oCupOnTable.Visible = true;
      return;
    }
    else{ //if not using blue cup
    player.Say("I'll keep this for now.");
    return;
    }
  }  
  player.Say("The table isn't particularly noteworthy.");
}

function hChair_AnyClick()
{
  if(player.ActiveInventory != null){ //if player uses inventory on hotspot
    unhandled_event(1, 3);
    return;
  }
  player.Say("If anyone is sitting there, they must be invisible. But that would take a lot of magic.");
  cRedMage.Say("No one is sitting there.");
}

function hTeakettle_AnyClick()
{
  if(player.ActiveInventory != null){ //if player uses inventory on hotspot
    unhandled_event(1, 3);
    return;
  }
  cRedMage.Say("The tea isn't ready yet.");
}

function hStove_AnyClick()
{
  player.Walk(324, 287, eBlock, eWalkableAreas);
  player.FaceDirection(eDirectionUp); //face the hotspot
  cRedMage.Say("Be careful, the stove is hot.");
  player.FaceDirection(eDirectionDown); //turn around
}

function hRug_AnyClick()
{
  if(player.ActiveInventory != null){ //if player uses inventory on hotspot
    player.Say("That would be very rude of me.");
    return;
    }
  player.Say("I like the colors.");
  cRedMage.Say("Why thank you.");
}

function hUpperExit_AnyClick()
{
  player.Walk(200, 146, eBlock, eWalkableAreas);
  player.Walk(155, 140, eBlock, eAnywhere);
  player.ChangeRoom(3, 90, 130, eDirectionDown); //go out onto balcony at specific coordinates
}

function hLight_AnyClick()
{
  if(player.ActiveInventory == null){ //if not using inventory on chandelier
    player.Say("The flicker of its flame can be mesmerizing.");
  }
  else{ //if using inventory
    player.Say("I can't reach it. And I should probably leave it alone anyway.");
  }
}

function hBed_AnyClick()
{
  if(player.ActiveInventory != null){ //if player uses inventory on hotspot
    unhandled_event(1, 3);
    return;
  }
  player.Say("It's a small space, but the bed looks very comfortable.");
  cRedMage.Say("It's all right.");
}

function hShelves_AnyClick()
{
  if(player.ActiveInventory == null){ // if not using inventory on shelves
    player.Say("Ancient books, orbs, and other arcane objects... a fitting collection for a master mage.");
    cRedMage.Say("Some of it is just for decoration. I'm more of a spell-caster than a user of... that stuff.");
  }
  else{  // if using inventory on shelves
    cRedMage.Say("Thank you, but please keep it.");
    cRedMage.Say("To be honest, I've got to clean some of these old things out.");
  }
}
function oCupOnTable_AnyClick()
{
  if(Mouse.Mode == eModeUseinv){
    unhandled_event(1, 3);
  }
  else{
    player.Walk(370, 290, eBlock, eWalkableAreas);
    oCupOnTable.Visible = false;
    gInGameMenu.Visible = true;         // Show the inventory GUI so that
    WaitMouseKey(GetGameSpeed());       // the player sees the item when it
    player.AddInventory(iBlueCup);      // becomes added. Add inventory item. Play powerup sound.
    aPowerup_by_maodin204.Play();       // Then hide the GUI afterward.
    WaitMouseKey(GetGameSpeed());       // 
    gInGameMenu.Visible = false;        //
  }
}
